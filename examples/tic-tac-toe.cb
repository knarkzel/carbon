include("std/arrays.cb")

BOARD := " ".repeat(3).repeat(3)

display_board := fn { 
  for row in BOARD {
    print(row.join(" | "))
  }
}

won_game := fn player {
  win := player.repeat(3)
  for board in [BOARD, BOARD.transpose()] {
    for row in board {
      if row == win {
        return true
      }
    }
  }
  diag := fn board {
    (0..2).map(fn i { board[i][i] })
  }
  diag(BOARD) == win || diag(BOARD.map(reverse)) == win
}

draw := fn {
  BOARD.all(fn row { !row.has(" ") })
}

player := "x"
game_over := false

while !game_over {
  display_board()
  pos := eval(prompt("player: " + player + ", position: ")) - 1
  BOARD[pos / 3][pos % 3] = player
  if won_game(player) { 
    display_board()
    print("game over, the winner is " + player + "!")
    game_over = true 
  } else if draw() {
    print("game over, it's a draw!")
    game_over = true
  }
  player = if player == "x" { "o" } else { "x" }
}
